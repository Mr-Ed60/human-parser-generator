grammar                 ::= { rule } ;

rule                    ::= identifier "::=" expressions ";" ;

expressions             ::= alternative-expressions
                          | sequential-expressions
                          ;

alternative-expressions ::= expression "|" expressions ;
sequential-expressions  ::= { expression } ;

expression              ::= string-expression
                          | extractor-expression
                          | optional-expression
                          | repetition-expression
                          | group-expression
                          | identifier-expression
                          ;

identifier-expression   ::= identifier ;
string-expression       ::= string ;
extractor-expression    ::= "/" regex "/" ;

optional-expression     ::= "[" expressions "]" ;
repetition-expression   ::= "{" expressions "}" ;
group-expression        ::= "(" expressions ")" ;

identifier              ::= /([A-Za-z][A-Z0-9a-z-]*)/ ;
string                  ::= /"([^"]*)"|^'([^']*)'/ ;
regex                   ::= /(.*?)(?<keep>/\s*;)/ ;
